#!/bin/sh
#
# Copyright (c) 2015 EMC Corporation
# All Rights Reserved
#

# Use: dumpex export_group_name
# This command will dump from Cassandra the Export Group, each Export Mask contained in
# the Export Group, and each Initiator contained in each Export Mask.
# It is useful for determining what masks were created and what initiators they contain.

awk -f - <<'MYEND' $*

# Parse the entries from a file fn and put in indicated array table.
# The keys are a concatenation of URI and property name,
# the contents are the value of the property.
function parseEntries(fn, table) {
    uri = 0;
    while (getline <fn > 0) {
        if (match($1, "id:")) {
            uri = $2;
	    #print "id: ", uri;
	    table[uri,"id"] = uri;
        } else if ($0 ~ "^$") {
	    uri=0;
	} else if (uri != 0) {
	    key=$1;
	    content = $0;
	    sub(".* = ", "", content)
	    table[uri, key] = content;
        }
    }
}

# Print all the properties of one object given by it's URI
# that is located in the indicated associative table.
function printobj(table, uri) {
    print "*************************************************************";
    print uri;
    for (id in table) {
        key=id;
        sub("(vdc.|global)$", "", key);
        if (key ~ uri) {
	    key=id;
	    sub("urn:.*\\:(vdc.|global)","",key);
	    sub("[^a-zA-Z0-9_]*", "", key);
            # Exclude the mongo large OpStatusMap and creationTime entries
            if (! ((key ~ "status" && table[id] ~ "OpStatusMap.*") || key ~ "creationTime")) {
	        print "    ", key, " = ", table[id];
            }
	}
    }
    print " ";
}

#
# Extract the properties of a give object given its
# URI and associative table and make a simplier
# properties / value map that is put in obj.
function getobj(table, uri, obj) {
    for (id in table) {
        key=id;
        sub("(vdc.|global)$", "", key);
        if (key ~ uri) {
	    key=id;
	    sub("urn:.*\\:(vdc.|global)","",key);
	    sub("[^a-zA-Z0-9_]*", "", key);
	    obj[key] = table[id];
	}
    }
}

BEGIN	{
    if (ARGC < 2) {
        print "usage: dumpex export_group_name";
        exit 1;
    }
    print ARGV[1];
    groupName = ARGV[1];
    system("/opt/storageos/bin/dbutils list ExportGroup >/tmp/_eg");
    system("/opt/storageos/bin/dbutils list ExportMask >/tmp/_em");
    system("/opt/storageos/bin/dbutils list Initiator >/tmp/_init");
    groups["0"] = "0";
    parseEntries("/tmp/_eg", groups);
    parseEntries("/tmp/_em", masks);
    parseEntries("/tmp/_init", inits);
    // Find the Export Group requested
    groupuri=0;
    for (id in groups) {
        # print id;
	key=id;
	sub("urn:.*\\:(vdc.|global)","",key);
	#print "key= ", key;
	if (key ~ "label") {
	    label=groups[id];
	    #print "label = ", label;
	    if (label ~ groupName) {
	        groupuri=id;
	        sub("label$", "", groupuri);
                printobj(groups, groupuri);
                getobj(groups, groupuri,egrp);
		exportMasks=egrp["exportMasks"];
		sub(".*\\[", "", exportMasks);
		sub("\\].*", "", exportMasks);
		split(exportMasks, masklist, ", ");
		for (maskid in masklist) {
		    printobj(masks, masklist[maskid]);
                    getobj(masks, masklist[maskid], mask);
		    initiators=mask["initiators"];
		    sub(".*\\[", "", initiators);
		    sub("\\].*", "", initiators);
                    split(initiators, initiatorlist, ", ");
		    for (initid in initiatorlist) {
		        printobj(inits, initiatorlist[initid]);
		    }
		}
	    }
	}
    }
    system("rm /tmp/_eg");
    system("rm /tmp/_em");
    system("rm /tmp/_init");
}
MYEND
