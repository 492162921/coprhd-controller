apply plugin: 'scripts'

scripts {
    geodbsvc {
        mainClass = "com.emc.storageos.db.server.impl.Main"
        debugPort = "8103"
        maxMemory = "9.8m"
        maxMemoryFactor = "0.1267"
        youngGenMemory = "68.3m"
        youngGenMemoryFactor = "0.0206"
        jvmArgs += " -XX:+UseThreadPriorities -XX:ThreadPriorityPolicy=42 -Xss228k -XX:+UseParNewGC"+
                   " -XX:+UseConcMarkSweepGC -XX:+CMSParallelRemarkEnabled -XX:SurvivorRatio=8 -XX:MaxTenuringThreshold=1"+
                   " -XX:CMSInitiatingOccupancyFraction=75 -XX:+UseCMSInitiatingOccupancyOnly"+
                   " -javaagent:\${LIB_DIR}/jamm-0.3.0.jar"
        jvmArgs += " -DbuildType=" + buildType
  
       String p = project.rootDir
       p += "/build/conf"
       sourceSets.test.runtimeClasspath = files(p) + sourceSets.test.runtimeClasspath
      }
}

configurations {
    jamm
}

dependencies {
    compile library(cglib),
            project(":dbsvc"),
            project(":dbclient")

    provided rootProject.toolsJar
    
    testCompile project(path:":dbsvc", configuration:'testRuntime')

    runtimeOnly project(":com.emc.sa.model")

    jamm "com.github.jbellis:jamm:0.3.0"
}

// These can only be run when vipr is not already running
task GeoDbTests(type:Test, dependsOn:compileTestJava) {
    forkEvery = 1
    maxHeapSize = '1024m'
    include "com/emc/storageos/db/gc/GlobalGarbageCollectorTests.class"
    include "com/emc/storageos/db/server/geo/DbClientGeoTest.class"
    include "com/emc/storageos/db/server/geo/DbGlobalLockTest.class"
    include "com/emc/storageos/db/server/geo/GeoDbSvcStartupTest.class"
}

test {
    forkEvery = 1
    maxHeapSize = '1024m'

    // These 4 tests kill the build when they fail because they call System.exit(), which needs to be fixed
    exclude "com/emc/storageos/db/gc/GlobalGarbageCollectorTests.class"
    exclude "com/emc/storageos/db/server/geo/DbClientGeoTest.class"
    exclude "com/emc/storageos/db/server/geo/DbGlobalLockTest.class"
    exclude "com/emc/storageos/db/server/geo/GeoDbSvcStartupTest.class"
}

